apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
  ext.mainClass = ''
}

mainClassName = 'com.nimsoc.application.Main'

repositories {
  mavenCentral()
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integration-test/java')
    }
    resources.srcDir file('src/integration-test/resources')
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  //outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

dependencies {
  implementation fileTree(dir: 'lib', include: ['*.jar'])
  implementation 'log4j:log4j:1.2.17'
  testImplementation 'junit:junit:4.13.2'
  integrationTestImplementation 'org.assertj:assertj-core:3.23.1'
}

task copyLicense {
  outputs.file new File("$buildDir/LICENSE")
  doLast {
    copy {
      from "LICENSE"
      into "$buildDir"
    }
  }
}

applicationDistribution.from(copyLicense) {
  into ""
}